import org.openapitools.generator.gradle.plugin.tasks.ValidateTask

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.2'
	id 'io.spring.dependency-management' version '1.1.2'
	id "com.github.johnrengelman.shadow" version "8.1.1"
	id "org.openapi.generator" version "6.6.0"
}

group = 'info.thale'
version = '0.0.1-SNAPSHOT'

springBoot {
	mainClass = 'info.thale.apiservice.ApiServiceApplication'
}

jar {
	manifest{
		attributes ( 'Main-Class': 'info.thale.apiservice.ApiServiceApplication')
	}
}

java {
	sourceCompatibility = '17'
}

sourceSets {
	main {
		java {
			srcDirs += "build/generate-resources/main/src/main"
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-jetty'
	implementation 'org.springframework.kafka:spring-kafka-dist:3.0.10'
	implementation 'org.springframework.kafka:spring-kafka:3.0.10'
	implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
	implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
	implementation 'jakarta.servlet:jakarta.servlet-api:5.0.0'
	implementation 'org.zalando:problem-spring-webflux:0.29.1'
	implementation 'org.zalando:jackson-datatype-problem:0.27.1'

	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	implementation 'io.swagger.core.v3:swagger-annotations:2.2.15'

	implementation 'javax.money:money-api:1.1'

	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	compileOnly 'org.projectlombok:lombok:1.18.28'
	annotationProcessor 'org.projectlombok:lombok:1.18.28'

	implementation 'org.springframework.boot:spring-boot-devtools'

	implementation 'org.mapstruct:mapstruct:1.5.5.Final'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

compileJava.dependsOn tasks.openApiGenerate

openApiValidate {
	inputSpec.set("$rootDir/src/main/resources/api-spec.yaml")
}

openApiGenerate {
	inputSpec.set("$rootDir/src/main/resources/api-spec.yaml")
	generatorName = "spring"
	library = "spring-boot"
	modelNameSuffix = "DTO"
	modelPackage = "info.thale.apiservice.api.generated.model"
	apiPackage = "info.thale.apiservice.api.generated"
	additionalProperties = [
	        "useSpringBoot3": true,
			"reactive": true,
			"useSpringController": true,
			"useTags": true,
			"useResponseEntity": true,
			"useOptional": true,
			"openApiNullable": false,
			"interfaceOnly": true
	]

}
